# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pyqtpurchasing
pkgname=(python-pyqtpurchasing)
pkgver=5.15.0
pkgrel=1
pkgdesc="Python bindings for QtPurchasing"
arch=(x86_64)
url="https://www.riverbankcomputing.com/software/pyqtpurchasing/intro"
license=(GPL3)
groups=(pyqt5)
depends=(qt5-purchasing python-pyqt5)
makedepends=(sip5 pyqt-builder)
source=("https://pypi.python.org/packages/source/P/PyQtPurchasing/PyQtPurchasing-$pkgver.tar.gz")
sha256sums=('88a37ceb8397cc88244a9e0de7e50d98489f9ed41d5feab46ce809b40cebe72f')

build() {
  cd "$srcdir"/PyQtPurchasing-$pkgver
  sip-build \
    --no-make \
    --api-dir /usr/share/qt/qsci/api/python
  cd build
  make
}

package_python-pyqtpurchasing(){
  cd PyQtPurchasing-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
