# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgver=22.04.62
pkgrel=2
pkgdesc="D-Bus interface for user account query and manipulation"
url="https://gitlab.freedesktop.org/accountsservice/accountsservice"
arch=(x86_64)
license=(GPL3)
depends=(glib2 polkit systemd shadow libxcrypt)
makedepends=(gtk-doc gobject-introspection git meson docbook-xsl xmlto vala)
checkdepends=(python-gobject python-dbusmock)
_commit=b92982be695700537bf80c5e9aa518820adbb7b6  # tags/22.04.62^0
source=("git+https://gitlab.freedesktop.org/accountsservice/accountsservice.git#commit=$_commit"
        0001-build-Properly-apply-modes-in-meson_post_install.py.patch)
sha256sums=('SKIP'
            '284b6564c352984a46b63f04eb964fcc7c18b7f89e45648a1bf7b2a90f68b6ff')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}


prepare() {
  cd $pkgname

  # Fix permissions to match the ones the daemon sets
  # https://gitlab.freedesktop.org/accountsservice/accountsservice/-/merge_requests/88
  git apply -3 ../0001-build-Properly-apply-modes-in-meson_post_install.py.patch
}

build() {
  local meson_options=(
    -D admin_group=wheel
    -D docbook=true
    -D gtk_doc=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libsystemd.so libcrypt.so)
  provides+=(libaccountsservice.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
