# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libetebase
pkgver=0.5.1
pkgrel=2
pkgdesc='Etebase C library'
arch=(x86_64)
url='https://github.com/etesync/libetebase'
license=(BSD)
depends=(openssl gcc-libs libsodium)
makedepends=(rust)
source=(https://github.com/etesync/libetebase/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('e98940d3f15a81609bb582c53428f3cb2871c7ea36693905e2229e86faf56438')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-$pkgver
  env CARGO_INCREMENTAL=0 SODIUM_USE_PKG_CONFIG= cargo build --release --locked --offline
  make pkgconfig
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
