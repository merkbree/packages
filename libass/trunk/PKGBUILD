# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: G_Syme <demichan@mail.upb.de>

pkgname=libass
pkgver=0.16.0
pkgrel=1
pkgdesc='A portable library for SSA/ASS subtitles rendering'
arch=(x86_64)
url=https://github.com/libass/libass/
license=(BSD)
depends=(
  fontconfig
  fribidi
  glib2
  glibc
  harfbuzz
  libfreetype.so
)
makedepends=(
  nasm
)
provides=(libass.so)
source=(https://github.com/libass/libass/releases/download/$pkgver/libass-$pkgver.tar.xz{,.asc})
validpgpkeys=(5458C3100671F252B0F4C7708079D18C21AAAAFF) # Oleg Oshmyan (Chortos-2) <chortos@inbox.lv>
b2sums=('d5d1a2492faaff671182b1ebdbdbab230529897575a485113a1025e73b3bdee1d5136f1dde9f5ded2bed3ad27fab78601d8f586499b5c7c3160f5d94a6357bea'
        'SKIP')

build() {
  cd libass-${pkgver}

  ./configure \
    --prefix=/usr \
    --enable-harfbuzz \
    --enable-fontconfig
  make
}

package() {
  cd libass-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libass/
}

# vim: ts=2 sw=2 et:
