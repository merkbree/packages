# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Thomas Kiss <thokis@gmail.com>

pkgname=sane-airscan
pkgver=0.99.9
pkgrel=2
pkgdesc="SANE - SANE backend for AirScan (eSCL) and WSD document scanners"
url="https://github.com/alexpevzner/sane-airscan"
arch=('x86_64')
license=('GPL2')
depends=('sane' 'libsoup')
#makedepends=('meson')
optdepends=('ipp-usb: allows to send HTTP requests via a USB connection on devices without Ethernet or WiFi connections')
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/alexpevzner/${pkgname}/archive/${pkgver}.tar.gz"
        Makefile.patch)
sha256sums=('2773a18007eb7b191fe02b30a82a20cf196f567fb32d4601a007f48ee6f34e21'
            '4b6677e7ed755afd3cf0e1bd1ca5c9426d252922ce7a2536d39db892da7f5446')
prepare() {
    cd ${pkgname}-${pkgver}
    # https://github.com/alexpevzner/sane-airscan/issues/53
    patch -Np1 -i ../Makefile.patch
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
    # add missing .so symlink
    cd "${pkgdir}"/usr/lib/sane
    ln -vsf libsane-airscan.so.1 libsane-airscan.so
}
