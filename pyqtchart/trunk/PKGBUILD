# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pyqtchart
pkgname=(python-pyqtchart)
pkgver=5.15.0
pkgrel=1
pkgdesc="Python bindings for QtCharts"
arch=(x86_64)
url="https://www.riverbankcomputing.com/software/pyqtchart/intro"
license=(GPL3)
groups=(pyqt5)
depends=(qt5-charts python-pyqt5)
makedepends=(sip5 pyqt-builder)
source=("https://pypi.python.org/packages/source/P/PyQtChart/PyQtChart-$pkgver.tar.gz")
sha256sums=('796b1a966759c78859d019bbac6b88b8e1c72ffebf65acf2be3cd9f45c756661')

build() {
  cd "$srcdir"/PyQtChart-$pkgver
  sip-build \
    --no-make \
    --api-dir /usr/share/qt/qsci/api/python
  cd build
  make
}

package_python-pyqtchart(){
  cd PyQtChart-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
