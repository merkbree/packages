# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=python-prometheus_client
pkgver=0.12.0
pkgrel=1

pkgdesc="Prometheus instrumentation library for Python applications"
url="https://github.com/prometheus/client_python"
arch=('any')
license=('APACHE')

depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-twisted' 'python-pytest')

source=(client_python-$pkgver.tar.gz::https://github.com/prometheus/client_python/archive/v${pkgver}.tar.gz)

sha512sums=('ebcbbed706a96a9402eb985f5da15ee8fae09de952144a89a3323e147c0d16615ccd9be58b50b5b8ede37d4f66de1e2ce93ed53c4da939ca2e44c7aac687a1f7')

build() {
  cd client_python-$pkgver
  python setup.py build
}

check() {
  cd client_python-$pkgver
  # Make reproducible by not writing pyc files for test files. (rebuilderd does not run tests)
  PYTHONDONTWRITEBYTECODE=1 python setup.py test
}

package() {
  cd client_python-$pkgver
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}
