# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Mark Rosenstand <mark@archlinux.org>

pkgname=fuse2
pkgver=2.9.9
pkgrel=4
pkgdesc='Interface for userspace programs to export a filesystem to the Linux kernel'
url='https://github.com/libfuse/libfuse'
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'fuse-common')
makedepends=('pkg-config')
provides=(fuse=$pkgver) # TODO: remove it once all packages start to use 'fuse2' name
replaces=('fuse')
conflicts=('fuse')
options=('!emptydirs' 'debug')
source=(https://github.com/libfuse/libfuse/releases/download/fuse-$pkgver/fuse-$pkgver.tar.gz{,.asc})
sha1sums=('943ba651b14bc4a3c6fd959ed4b8c04f4a59032d'
          'SKIP')
validpgpkeys=(ED31791B2C5C1613AF388B8AD113FCAC3C4E599F) # Nikolaus Rath <Nikolaus@rath.org>

build() {
  cd fuse-$pkgver

  UDEV_RULES_PATH=/usr/lib/udev/rules.d MOUNT_FUSE_PATH=/usr/bin ./configure --prefix=/usr --libdir=/usr/lib --enable-lib --enable-util --disable-example
  make
}

package() {
  cd fuse-$pkgver

  make DESTDIR="${pkgdir}" install

  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -r "${pkgdir}"/etc/init.d

  # static device nodes are handled by udev
  rm -r "${pkgdir}"/dev
}
