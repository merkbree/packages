# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=eventviews
pkgver=21.12.1
pkgrel=1
pkgdesc='Library for creating events'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(calendarsupport kdiagram)
makedepends=(extra-cmake-modules boost qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('a3f13127220428dd3519dae7d5dd4fb10cc234145dfe36be1481c564d085cbbc'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
