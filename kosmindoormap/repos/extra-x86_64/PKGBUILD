# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kosmindoormap
pkgver=21.12.1
pkgrel=1
pkgdesc='OSM multi-floor indoor map renderer'
arch=(x86_64)
url='https://www.kde.org'
license=(LGPL)
depends=(kpublictransport kopeninghours protobuf)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('13f58f1f76fb3a80b72279c033d8302b7de9fbd16b864f117e9078ff2203327f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
