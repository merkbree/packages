# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.3.1
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=('any')
url="https://github.com/archlinux/archinstall"
license=('GPL3')
depends=('python')
makedepends=('python-setuptools' 'python-sphinx')
provides=('python-archinstall')
conflicts=('python-archinstall')
replaces=('python-archinstall')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig")
sha512sums=('5a7890dafff716f4d58ca12739017ebd8fca11105707ba2fa8bcf426f201889cf33c2906ca03e34aee019e85c17584366e8a9254b586b74dc27c1d027ca79305'
            'SKIP')
b2sums=('d1a49d645f165de8164896cae3f38329065522a18973a6cb8b39062369f0a6f997968ebae877437d7e3663f034902ea9334feb956b57b57c28ce04bdeabba775'
        'SKIP')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A') # Anton Hvornum (Torxed) <anton@hvornum.se>


build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  make man -C docs
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -vDm 644 docs/_build/man/archinstall.1 -t "${pkgdir}/usr/share/man/man1/"
}
