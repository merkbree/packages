# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=falkon
pkgver=3.2.0
pkgrel=2
pkgdesc='Cross-platform QtWebEngine browser'
arch=(x86_64)
url='https://www.falkon.org/'
license=(GPL3)
depends=(qt5-webengine qt5-x11extras qt5-svg karchive)
makedepends=(extra-cmake-modules qt5-tools kio purpose libgnome-keyring pyside2 shiboken2)
optdepends=('kio: KDE integration'
            'purpose: KDE integration'
            'libgnome-keyring: gnome keyring integration'
            'pyside2: Python plugins')
source=(https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f4ff925b7572b6e035424face86d05ca2df66723061bc57f2b1615c2c09012aa'
            'SKIP')
validpgpkeys=(3CB7B8416EF6778B409FDC43EBC3FC294452C6D8  # David Rosca <nowrep@gmail.com>
              E0A3EB202F8E57528E13E72FD7574483BB57B18D) # Jonathan Esk-Riddell <jr@jriddell.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
