# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-wayland-protocols
pkgver=1.6.0
pkgrel=1
pkgdesc='Plasma Specific Protocols for Wayland'
arch=(any)
url='https://kde.org/plasma-desktop'
license=(LGPL)
depends=()
makedepends=(qt5-base extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('b74ffac969ef067e47180e748a67a3a1816b7157ff4ea620ed4432f59b70f01f'
            'SKIP')
validpgpkeys=(CA1B4E348D22755A67EF4E1A6A4F2C4CDC8BC77F  # Aleix Pol Gonzalez <aleixpol@kde.org>
              EC5516B514543DA145169FD12276F09AF4A790F4) # Vlad Zahorodnii <vlad.zahorodnii@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
