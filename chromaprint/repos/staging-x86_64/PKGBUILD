# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Wieland Hoffmann <the_mineo@web.de>

pkgname=chromaprint
pkgver=1.5.1
pkgrel=2
pkgdesc="Library for extracting fingerprints from any audio source"
url="https://acoustid.org/chromaprint"
arch=('x86_64')
license=('LGPL2.1' 'MIT')
depends=('gcc-libs' 'glibc')
makedepends=('cmake' 'ffmpeg' 'gtest')
provides=('libchromaprint.so')
# upstream signs with DSA key: https://github.com/acoustid/chromaprint/issues/81
source=(https://github.com/acoustid/${pkgname}/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        ffmpeg5.patch)
sha512sums=('ea16e4d2b879c15b1d9b9ec93878da8b893f1834c70942663e1d2d106c2e0a661094fe2dd3bae7a6c2a1f9d5d8fab5e0b0ba493561090cf57b2228606fad1e66'
            '2d44d4ce2f070e48c1600b8eca386e5610262084aa1de83e46adcd2154fc178faed95a66a3f2d0b8519faa2bae666e6f7337e8a364c04e87cd5c325cbbd2328f')
b2sums=('9f7f030e97d3114cf679df298d313ea826c0fb05e7e7d8a10090d0a27ed0811b380b81b29fce973e0493826c478964367396311fd0484619cb2fc4c2d8e0d4c0'
        '9e67be84d26a69916e1846533e98808044412d495abb7a5725141dd81833ac87992ba4a0a49e804c3c7ffe7b01dc2b9e112d6109643654f7fd33f422042bc3a4')

prepare() {
  patch -d $pkgname-$pkgver -p1 < ffmpeg5.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DBUILD_TESTS=ON \
        -DBUILD_TOOLS=ON \
        -DGTEST_SOURCE_DIR=/usr/src/googletest/ \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build check
}

package() {
  depends+=('libavcodec.so' 'libavformat.so' 'libavutil.so' 'libswresample.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="${pkgdir}" install -C build
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {NEWS.txt,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
