# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Generator : pbjam 0.01

pkgname=perl-http-message
pkgver=6.35
pkgrel=1
pkgdesc="HTTP style messages"
arch=('any')
url='https://search.cpan.org/dist/HTTP-Message'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-clone' 'perl-encode-locale' 'perl-http-date' 'perl-io-html'
         'perl-lwp-mediatypes' 'perl-uri')
makedepends=('perl-try-tiny')
conflicts=('perl-libwww<6')
source=("https://search.cpan.org/CPAN/authors/id/O/OA/OALDERS/HTTP-Message-$pkgver.tar.gz")
sha512sums=('9b4571062c8d268581219115cd73e2fa9a6b2bcfeb474f8419d80326846c5aaedc0f6a9982a20dfb604a30e295a20bbc0192992cbfedc3f8aa0e59dfbfe653c2')

build() {
  cd HTTP-Message-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd HTTP-Message-$pkgver
  make test
}

package() {
  cd HTTP-Message-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
