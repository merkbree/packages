# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-alien-build
pkgver=2.64
pkgrel=1
pkgdesc="Build external dependencies for use in CPAN"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Alien-Build"
depends=('perl-capture-tiny' 'perl-ffi-checklib' 'perl-file-chdir' 'perl-file-which'
         'perl-path-tiny')
makedepends=('perl-test2-suite')
checkdepends=('perl-alien-base-modulebuild' 'perl-alien-cmake3' 'perl-env-shellwords'
              'perl-pkgconfig' 'perl-pkgconfig-libpkgconf' 'perl-readonly' 'perl-sort-versions')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Alien-Build-$pkgver.tar.gz")
sha512sums=('54904d59c8172e5f29dd33bf94b08a61deec264f844fbfb2db9faa4aaf82eabeaf39fdcb4f5f46e80dc8806d3e13736cfae34aaa5d8632cf5ff2505bed9f176b')

build() {
  cd Alien-Build-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Alien-Build-$pkgver
  make test
}

package() {
  cd Alien-Build-$pkgver
  make DESTDIR="$pkgdir" install
}
