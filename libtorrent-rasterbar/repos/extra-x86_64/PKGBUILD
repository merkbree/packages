# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=2.0.5
pkgrel=1
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="https://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('BSD')
depends=('boost-libs' 'openssl')
makedepends=('boost' 'cmake' 'ninja' 'python-setuptools')
options=('!emptydirs')
source=(https://github.com/arvidn/libtorrent/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/arvidn/libtorrent/commit/71d608fc.patch)
sha512sums=('be5b812135dada957e565085b5bdda06827c8427f78a4468ef263e1a1e33d3a0bbba7ac27235f0d17ae6087d54311281e3b1975eef81cda514acc8329862dc04'
            'ba7788c3b66373b0b11c47cd87b91dddad1898e83eef6269b69c6beb27adb0bdf2e9570a1a6ad731dd907f28b15d6739f5a4cb303a1f99bd5ed3b5cd6fc1edf6')

prepare() {
  mkdir -p build
  cd $pkgname-$pkgver
  patch -p1 -i ../71d608fc.patch # Fix build with boost 1.78
}

build() {
  cd build
  cmake -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_INSTALL_LIBDIR="lib" \
    -Dpython-bindings=ON \
    -Dboost-python-module-name="python" \
    -Dpython-egg-info=ON \
    -GNinja ../$pkgname-$pkgver
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
  install -Dm644 ../$pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
