# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libopenmpt
pkgver=0.6.1
pkgrel=1
pkgdesc="A library to render tracker music to a PCM audio stream"
arch=(x86_64)
url="https://lib.openmpt.org/libopenmpt/"
license=(BSD)
depends=(
  flac libFLAC.so
  gcc-libs
  glibc
  libogg libogg.so
  libpulse libpulse.so libpulse-simple.so
  libsndfile libsndfile.so
  libvorbis libvorbis.so libvorbisfile.so
  mpg123 libmpg123.so
  portaudio libportaudio.so
)
makedepends=(autoconf-archive doxygen help2man)
# TODO: remove provides/replaces on openmpt123 after 2022-05-01
provides=(libopenmpt.so openmpt123)
replaces=(openmpt123)
source=(
  "$pkgname-$pkgver.tar.gz::https://lib.openmpt.org/files/libopenmpt/src/${pkgname}-${pkgver}+release.autotools.tar.gz"
)
sha512sums=('b43124746fc7c8bdbcfcf24c5cff1cd8330cab664fd1641ac7a35416ed25bb80c74f31db74085c13f4beb9774c17c12a4486c8c5e976f3fbb70a27c236c0f4fb')
b2sums=('222e71f0cc1343ff76c9ffc295664c52db74906b20a9c7cdd7d7e7d05a4795a2cda1d0de265f0e1b2dabc1bfb6757342046117222b5d035f633ae9da7e7d2301')

prepare() {
  cd "$pkgname-$pkgver+release.autotools"
  autoreconf -fiv
}

build() {
  cd "$pkgname-$pkgver+release.autotools"
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k check -C "$pkgname-$pkgver+release.autotools"
}

package() {
  cd "$pkgname-$pkgver+release.autotools"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
