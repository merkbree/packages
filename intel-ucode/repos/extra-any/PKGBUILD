# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=intel-ucode
pkgver=20200609
pkgrel=2
pkgdesc='Microcode update files for Intel CPUs'
arch=('any')
url='https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files'
replaces=('microcode_ctl')
makedepends=('iucode-tool')
license=('custom')
source=("${pkgname}-${pkgver/./}.tar.gz::https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files/archive/microcode-${pkgver/./}.tar.gz"
        'intel-ucode-06-4e-03::https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files/raw/437f382b1be4412b9d03e2bbdcda46d83d581242/intel-ucode/06-4e-03')
sha256sums=('6c5295265abd03a7cdc815b85bff4f98387f813826a88935904bc2bbc783d5e4'
            '0e4d61493592bcd6acb9f2b5047cd1eede17dad5e3a2bfe8e2774c4099ab53df')

prepare() {
  # https://bugs.archlinux.org/task/6449://bugs.archlinux.org/task/66978
  # https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files/issues/31
  install -m0644 intel-ucode-06-4e-03 Intel-Linux-Processor-Microcode-Data-Files-microcode-${pkgver/./}/intel-ucode/06-4e-03
}

build() {
  cd Intel-Linux-Processor-Microcode-Data-Files-microcode-${pkgver/./}

  rm -f intel-ucode{,-with-caveats}/list
  mkdir -p kernel/x86/microcode
  iucode_tool --write-earlyfw=intel-ucode.img intel-ucode{,-with-caveats}/
}

package() {
  cd Intel-Linux-Processor-Microcode-Data-Files-microcode-${pkgver/./}

  install -D -m0644 intel-ucode.img "${pkgdir}"/boot/intel-ucode.img
  install -D -m0644 license "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
