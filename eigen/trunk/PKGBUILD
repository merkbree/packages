# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Kringel

pkgname=eigen
pkgver=3.3.7
pkgrel=6
pkgdesc="Lightweight C++ template library for vector and matrix math, a.k.a. linear algebra."
arch=('any')
url='https://eigen.tuxfamily.org'
license=('GPL3' 'LGPL2.1' 'BSD' 'custom:MPL2' 'custom:MINPACK')
makedepends=('cmake' 'pkg-config' 'freeglut' 'gcc-fortran' 'fftw' 'suitesparse' 'boost')
replaces=('eigen3')
provides=('eigen3')
source=("${pkgname}-${pkgver}.tar.bz2::https://bitbucket.org/eigen/eigen/get/${pkgver}.tar.bz2"
         eigen-c++17.patch)
sha256sums=('9f13cf90dedbe3e52a19f43000d71fdf72e986beb9a5436dddcd61ff9d77a3ce'
            'd62ece0e05e4fc85282ff462d86b08dc85ff16e991d7ae4ddf24c3f33fbe8c49')

prepare() {
  mkdir -p build

  cd eigen-eigen-*
  patch -p1 -i ../eigen-c++17.patch # https://gitlab.com/libeigen/eigen/-/issues/1676  
}

build() {
  cd build
  cmake ../eigen-eigen-* \
    -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  # install custom licenses
  install -Dm644 ../eigen-eigen-*/COPYING.MPL2 -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 ../eigen-eigen-*/COPYING.BSD -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 ../eigen-eigen-*/COPYING.MINPACK -t "$pkgdir/usr/share/licenses/$pkgname"
}
