# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksysguard
pkgver=5.19.0
pkgrel=2
pkgdesc='Libraries for ksysguard'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(libxres qt5-webengine kdeclarative knewstuff)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
         libksysguard-dbus-name.patch::"https://invent.kde.org/plasma/libksysguard/-/commit/27da06df.patch")
sha256sums=('f026b65372aa49b1f72a7c8029cb3d7afd131de9592cc5392fe05771a4da26d3'
            'SKIP'
            '50054408ed31642bd543f6e103cd7fb241ad1eba74102c68269c4d0459b396c6')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../libksysguard-dbus-name.patch # Fix ksystemstats dbus interface name
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
