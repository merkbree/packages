# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgname=python2-isort
pkgver=4.3.16
pkgrel=1
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
depends=('python2-futures' 'python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/timothycrosley/isort/archive/$pkgver.tar.gz")
sha512sums=('74729c4e4305d57e210b9e1c9c97053e7d8971018df3555e111e74bf65b03219309382fbecf5add082b47ec4bbc7b91fc29bef398908efa7bfb1383e3b3cef1e')

build() {
  cd isort-$pkgver
  python2 setup.py build
}

package() {
  cd isort-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/isort{,2}
}
