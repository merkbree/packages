# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
_pkgver=4.3.0-beta2
pkgver=${_pkgver/-/}
pkgrel=1
pkgdesc="Edit and paint images"
arch=(x86_64)
url="https://krita.org"
license=(GPL3)
depends=(kitemviews kitemmodels ki18n kcompletion kguiaddons kcrash qt5-svg qt5-multimedia quazip
         gsl libraw exiv2 openexr fftw boost-libs giflib openjpeg2 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost eigen vc poppler-qt5 opencolorio python-pyqt5 libheif
             qt5-tools sip python-sip)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations' 'opencolorio: for the LUT docker'
            "krita-plugin-gmic: G'MIC plugin" 'python-pyqt5: for the Python plugins' 'libheif: HEIF filter')
conflicts=(calligra-krita krita-l10n)
replaces=(calligra-krita krita-l10n)
source=("https://download.kde.org/unstable/krita/$_pkgver/$pkgname-$_pkgver.tar.gz"
         krita-pyqt5-sip5.patch)
sha256sums=('5de9dcc9ee95b5ffdc14997d4a77f5e15462e099f237740c1beff46b2ac3808e'
            'ab2f33843d8cad31bc13e0eca7dc732cdbfed054924f25ed61102c0d0971c1d8')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD') # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build

  cd $pkgname-$_pkgver
  patch -p1 -i ../krita-pyqt5-sip5.patch # Fix sip include dir when PyQt5 is compiled with SIP 5
}

build() {
  cd build
  cmake ../$pkgname-$_pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
