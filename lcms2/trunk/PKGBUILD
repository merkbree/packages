# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=lcms2
pkgver=2.10
pkgrel=2
pkgdesc='Small-footprint color management engine, version 2'
arch=('x86_64')
url='http://www.littlecms.com/'
license=('MIT')
depends=('libtiff')
provides=('liblcms2.so')
source=("https://downloads.sourceforge.net/sourceforge/lcms/${pkgname}-${pkgver}.tar.gz")
sha256sums=('50d411fd494c7c6973866e08c05dea83245d7e23a0db6237a9d00f88b2e0f346')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
